apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
android {
    compileSdkVersion 32
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.fitnesswell"
        minSdkVersion 22
        targetSdkVersion 32
        versionCode 63
        versionName "0.20.5.1"
        vectorDrawables.useSupportLibrary true
    }

    signingConfigs {
        if (rootProject.file('release.properties').exists()) {
            def props = new Properties()
            props.load(new FileInputStream(rootProject.file('release.properties')))
            release {
                storeFile file(props.keyStore)
                storePassword props.keyStorePassword
                keyAlias props.keyAlias
                keyPassword props.keyAliasPassword
            }
        }
        if (rootProject.file('debug.properties').exists()) {
            def props2 = new Properties()
            props2.load(new FileInputStream(rootProject.file('debug.properties')))
            debug {
                storeFile file(props2.keyStore)
                storePassword props2.keyStorePassword
                keyAlias props2.keyAlias
                keyPassword props2.keyAliasPassword
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue("bool", "FIREBASE_CRASH_ENABLED", "true")
            if (rootProject.file('release.properties').exists()) {
                signingConfig signingConfigs.release
            }
            buildConfigField "Boolean", "DEBUG_MODE", "false"
            resValue "string", "app_name", "FitnessWell"
        }
        debug {
            resValue("bool", "FIREBASE_CRASH_ENABLED", "false")
            if (rootProject.file('debug.properties').exists()) {
                signingConfig signingConfigs.debug
            }
            buildConfigField "Boolean", "DEBUG_MODE", "true"
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "FitnessWell"
        }
    }

    buildFeatures {
        dataBinding true
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }
}

dependencies {
    // Support
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.3'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'com.google.android.material:material:1.6.1'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:1.10.19'

    // Other
    implementation 'com.github.JDevZone:FillProgressLayout:0.0.8'
    implementation 'com.kovachcode:timePickerWithSeconds:1.0.1'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation 'com.ogaclejapan.smarttablayout:library:2.0.0@aar'
    implementation 'com.ogaclejapan.smarttablayout:utils-v4:2.0.0@aar'
    implementation 'com.github.lzyzsd:circleprogress:1.2.1'
    implementation 'com.heinrichreimersoftware:material-intro:2.0.0'
    implementation 'com.mikhaellopez:circularimageview:3.2.0'
    implementation 'com.github.f0ris.sweetalert:library:1.6.2'
    implementation 'com.github.onurkagan:ktoast:1.0.3'
    implementation 'com.theartofdev.edmodo:android-image-cropper:2.8.0'
    implementation 'com.github.ivbaranov:materialfavoritebutton:0.1.5'
    implementation project(':progressviewslib')
    implementation 'jp.wasabeef:recyclerview-animators:4.0.2'
    implementation 'com.squareup.picasso:picasso:2.71828'
    // Import the Firebase BoM
    implementation platform('com.google.firebase:firebase-bom:30.1.0')


    implementation 'com.google.firebase:firebase-firestore'
    // Add the dependency for the Firebase SDK for Google Analytics
    // When using the BoM, don't specify versions in Firebase dependencies
    implementation 'com.google.firebase:firebase-analytics'

    // Declare the dependency for the Firebase Authentication library
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation 'com.google.firebase:firebase-auth'

    implementation 'com.google.firebase:firebase-storage'


}
